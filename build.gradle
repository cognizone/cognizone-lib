buildscript {
  ext {
    commonsLang3Version = "3.12.0"
  }
}

plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.13.3'
  id 'org.springframework.boot' version '2.6.14' apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'idea'
  id 'java'
  id 'java-library'
  id 'maven-publish'
  id "io.freefair.lombok" version "6.5.1"
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://repo.spring.io/milestone" }
  maven { url "https://archiva.cogni.zone/repository/public" }
}

scmVersion {
  tag {
    prefix = "v"
    versionSeparator = ""
    branchPrefix = [
            'release/.*': 'release-v',
            'hotfix/.*' : 'hotfix-v'
    ]
    initialVersion = { t,p -> '0.0.1' }
  }
  nextVersion {
    suffix = "SNAPSHOT"
    separator = "-"
  }
}

idea {
  project {
    jdkName = "11"
    languageLevel = "11"
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: "io.freefair.lombok"
  apply plugin: 'io.spring.dependency-management'
  apply plugin: "maven-publish"

  group = 'zone.cogni.lib'
  version = scmVersion.version
  sourceCompatibility = '11'

  repositories {
    if (project.hasProperty('jenkins-ci')) {
      maven {
        url "${System.properties['nexus.url']}/repository/cognizone-group"
        credentials {
          username = System.properties['nexus.username']
          password = System.properties['nexus.password']
        }
        allowInsecureProtocol(true)
      }
    }
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://archiva.cogni.zone/repository/public" }
  }

  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
    dependencies {
      dependency("org.apache.commons:commons-lang3:${commonsLang3Version}")
    }
  }

  dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
  }

  task sourcesJar(type: Jar) {
    description = 'Creates sources JAR.'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
  }

  tasks.named('test') {
    useJUnitPlatform()
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        groupId = group
        version = scmVersion.version
        from components.java
        artifact sourcesJar
      }
    }
    repositories {
      if (project.hasProperty("publishToCognizoneArchiva")) {
        maven {
          credentials {
            username = System.properties['archiva.username']
            password = System.properties['archiva.password']
          }
          name = 'cognizone'
          url = System.properties['archiva.url']
        }
      }
      if (project.hasProperty("publishToCognizoneNexus")) {
        maven {
          credentials {
            username = System.properties['nexus.username']
            password = System.properties['nexus.password']
          }
          def releasesRepoUrl = "${System.properties['nexus.url']}/repository/cognizone-release"
          def snapshotsRepoUrl = "${System.properties['nexus.url']}/repository/cognizone-snapshot"
          url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
          allowInsecureProtocol(true)
        }
      }
    }
  }
}

project("httpheaders") {
  dependencies {
    implementation("javax.inject:javax.inject:1")
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation("org.springframework.boot:spring-boot-starter-validation")
  }
}

project("methodtimer") {
  dependencies {
    implementation("javax.inject:javax.inject:1") //if we don't have this dependency the publication build does not work???
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.apache.commons:commons-lang3")
  }
}
